plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "com.github.node-gradle.node" version "3.0.1"
}

repositories {
        mavenCentral()

}



group = 'fem.coders.app'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'



dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}



test {
    useJUnitPlatform()
}

task installFrontendDependencies(type: NpmTask) {
    workingDir = file('./web-client')
    args = ['install']
}

task compileWebClient(type: NpmTask) {
    dependsOn("installFrontendDependencies")
    workingDir = file('./web-client')
    args = ['run', 'build']
}

task copyFiles(type: Copy) {
    dependsOn("compileWebClient")
    from('./web-client/build')
    include("*.html")
    include("*.js")
    include("*.css")
    into("./src/main/resources/static")
}


task buildFrontend(dependsOn: [
        'compileWebClient',
        'copyFiles'
])

build.dependsOn buildFrontend

